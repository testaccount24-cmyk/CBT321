*ASM XOPTS(SP)
***********************************************************************
*                                                                     *
* Name:         SYSEXCIS                                              *
*                                                                     *
* Author:       Roland Schiradin                                      *
*                                                                     *
* Date:         December 1996                                         *
*                                                                     *
* Descrition:   Cics ExCi-server for Ceda-Commands                    *
*                                                                     *
*                                                                     *
***********************************************************************
*=====================================================================*
*                                                                     *
*      Working Storage.                                               *
*                                                                     *
*=====================================================================*
*
DFHEISTG DSECT  ,                    |Program Variables.             |
EYECATCHER   DC CL8'WSTORAGE'        |                               |
IO_FLAG      DS X'00'                |  Flag indicate Output of ceda |
CEDA_PARAMETER DS 0F                 |  Ceda_Parameter               |
CEDA_PARM1     DS F                  |    First Parm                 |
CEDA_PARM2     DS F                  |    Second Parm                |
CEDA_PARM3     DS F                  |    Third Parm                 |
CEDA_PARM4     DS F                  |    Fourth Parm                |
CEDA_PARM5     DS F                  |    Fifth Parm                 |
*=====================================================================*
*                                                                     *
*      Commarea DSECT.                                                *
*                                                                     *
*=====================================================================*
*
COMMAREA DSECT
L_COMMAREA_COMM DS H
COMMAREA_COMM DS  CL94               | CommArea-Command
L_COMMAREA_DATA DS H
COMMAREA_DATA DS  CL3998             | CommArea-Data
*
SYEXCIS  DFHEIENT CODEREG=(R3),DATAREG=(R6,R7)
*
*=====================================================================*
*   Check that a COMMAREA has been passed. If we don't have a commarea*
*   we cannot even return a return code back to the client, so issue  *
*   an abend instead.                                                 *
*=====================================================================*
         CLC   EIBCALEN,=H'0'
         BNE   PARSE
         EXEC CICS ABEND ABCODE('NCOM')
*
*=====================================================================*
*   Map the COMMAREA DSECT onto the Commarea passed.                  *
*=====================================================================*
PARSE    DS    0H
         EXEC CICS ADDRESS COMMAREA(R4)
         USING COMMAREA,R4              tell assembly
*
*=====================================================================*
*   Setup the Parmeterlist                                            *
*=====================================================================*
         LA    R5,COMMAREA_COMM         setup the PLIST
         ST    R5,CEDA_PARM1
         LA    R5,L_COMMAREA_COMM
         ST    R5,CEDA_PARM2
         LA    R5,IO_FLAG
         ST    R5,CEDA_PARM3
         LA    R5,COMMAREA_DATA
         ST    R5,CEDA_PARM4
         LA    R5,L_COMMAREA_DATA
         ST    R5,CEDA_PARM5
         OI    CEDA_PARM5,X'80'         mark as last parm
*
*=====================================================================*
*   Set parms                                                         *
*=====================================================================*
         MVI   IO_FLAG,X'00'            avoid Terminalputput
*
*=====================================================================*
*   Link to DfhEdap                                                   *
*=====================================================================*
         EXEC CICS LINK PROGRAM('DFHEDAP') COMMAREA(CEDA_PARAMETER)
*
*=====================================================================*
*   All calls return to here.  Control is passed back to the Client   *
*=====================================================================*
*
RETURN   DS    0H
         EXEC CICS RETURN
BLANKS   DC  CL256' '
*
         DFHREGS
         END SYEXCIS

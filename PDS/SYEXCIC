000200 Identification Division.
000300 Program-Id. SYEXCIC.
000400****************************************************************
000500*                                                              *
000600*  PROGRAM NAME: SYEXCIC                                       *
000700*                                                              *
000800*  TITLE: Ceda Commands from Batch                             *
000900*                                                              *
001000*  DATE: 28.12.96                                              *
001100*                                                              *
002100*                                                              *
002200*  INPUT:         The target CICS system with which this       *
002300*                 batch client program communicates can be     *
002400*                 specified via the JCL PARM statement,        *
002500*                 otherwise it defaults to applid DBDCCICS.    *
002600*                 The target file is hardcoded as FILEA.       *
002700*                                                              *
002800*  OUTPUT:        The whole of the Target File will be output  *
002900*                 to the SYSPRINT log, together with status    *
003000*                 information.                                 *
003100*                                                              *
003200****************************************************************
003300 Environment Division.
003400*==============================================================*
003500 Input-Output Section.
003600 File-Control.
003700     Select Printer assign to SYSPRINT.
003710     Select Eingabe assign to SYSIN.
003800 Data Division.
003900*==============================================================*
004000 File Section.
004100*==============================================================*
004200 fd  Printer
004210     Block contains 128 Characters
004300     Recording Mode S
004400     Label Records omitted.
004500 01  Output-Record      pic x(128).
004501 fd  Eingabe
004510     Block contains 80 Characters
004520     Recording mode f.
004540 01  Input-Record.
004550     05 I-Target-Applid pic x(8).
004560     05 I-Csd-Group     pic x(8).
004570     05 Filler          pic x(64).
004600 Working-Storage Section.
004700*==============================================================*
004800*   Declare Call level,DPL, and EXEC level Return Code areas.  *
004900*==============================================================*
005000 COPY DFHXCPLO.
005100*==============================================================*
005200*   Declare Areas to be used for outputting return codes.      *
005300*==============================================================*
005400 01  Output-RetArea.
005500   05                   pic x(20)  value Spaces.
005600   05  O-Resp           pic 9(8).
005700   05                   pic xx     value Spaces.
005800   05  O-Resp2          pic 9(8).
005900   05                   pic xx     value Spaces.
006000   05  OExci-Sub-Reason1 pic 9(8).
006100   05  O-AbCode-Line    redefines OExci-Sub-Reason1.
006200       10  O-AbCode     pic x(4).
006300       10  Opad-AbCode  pic x(4).
006400   05                   pic x(80) value Spaces.
006500 01  Sub                pic s9(8) comp.
006510 01  Ceda-Sev-Num       pic 9(2)  value 0.
006520 01  Ceda-Sev-Highest   pic 9(2)  value 0.
006600 01  Out-Rec.
006700   05 Out-Rec-Elem      pic x occurs 128 times.
008100*==============================================================*
008200*   Initialise Target information variables.                   *
008300*==============================================================*
008500 01  Target-Program     pic x(8)       value 'SYEXCIS '.
008600 01  Target-Transid     pic x(4)       value 'EXCI'.
009500*==============================================================*
009600*   Define Commarea struct.                                    *
009700*==============================================================*
009800 01  Commarea.
009801   03  Commarea-Data.
009810     05  Length-Commarea-Comm pic s9(4)  comp.
009820     05  Commarea-Comm.
009900       10  Cics-Transaction pic x(4)     value 'CEDA'.
009910       10                   pic x        value Spaces.
010000       10                   pic x(10)    value 'INSTALL G('.
010100       10  Cics-Tran-Ext    pic x(8)     value Spaces.
010110       10                   pic x        value ')'.
010111       10                   pic x(70)    value Spaces.
010112     05  Length-Commarea-Rcv pic s9(4)   comp.
010120   03  Commarea-Rcv.
010130     05                   pic s9(4)      comp.
010140     05                   pic s9(4)      comp.
010150     05                   pic s9(4)      comp.
010160     05                   pic s9(4)      comp.
010170     05 No-of-Msgs        pic s9(4)      comp.
010180     05 Ceda-Severity     pic s9(4)      comp.
010190     05                   pic x.
010200     05 Ceda-Message      pic x(80).
010300     05                   pic x(3905).
011000*==============================================================*
011100*   Initialise program specific variables and flags.           *
011200*==============================================================*
011400 01  Program-Flags.
011500   05  Abort-Flag       pic x(3)       value 'NO '.
011600       88 Abort                        value 'YES'.
011700       88 No-Abort                     value 'NO '.
011800   05  Eof-Flag         pic x(3)       value 'NO '.
011810       88 Eof                          value 'YES'.
011820       88 No-Eof                       value 'NO '.
011900 01  Program-Messages.
012000   05  Msg00  pic x(128) value Spaces.
012100   05  Msg01  pic x(128) value '*===================== Batch CSD-I
012200-     'nstall ======================================*'.
012300   05  Msg02  pic x(128) value '*
012400-     '                                             *'.
012500   05  Msg03  pic x(128) value '*  Following Command will be proce
012600-     'ed:                                          *'.
014100   05  Msg11  pic x(128) value '*    The Link Request has failed.
014200-     ' Return codes are:                           *'.
014300   05  Msg13  pic x(128) value '*    A message was received from t
014400-     'he target CICS system:                       *'.
014500   05  MSg14  pic x(128) value '*    >>>> Aborting further process
014600-     'ing <<<<                                     *'.
017700   05  Msg31  pic x(128) value '*=================== End of Batch
017800-     'CSD-Install =================================*'.
017900*==============================================================*
018000 linkage section.
018100   01  Null-ptr  usage is Pointer.
018200*
018900   01  Exec-Level-Msg.
019000     05 Exec-Level-Msg-Text pic x occurs 128 times.
019100*
019500
019600 Procedure Division.
019700     Open output Printer
019710     Open input Eingabe
019720     Write Output-Record from Msg00
019730     Write Output-Record from Msg01
019740     Write Output-Record from Msg02
019800*==============================================================*
019900*    Get data from Eingabe                                     *
020000*==============================================================*
020010     Perform until eof or abort
020100       Read Eingabe
020110         at end
020120           Set Eof to true
020130       End-Read
020140       If eof
020141         Continue
020142       else
023200*        Set up the Commarea for transmission.
023600         Move Spaces         to   Commarea-Rcv
023610         Move I-Csd-Group    to   Cics-Tran-Ext
023620         Move Length of CommArea-Comm to Length-Commarea-Comm
023630         Move Length of CommArea-Rcv  to Length-Commarea-Rcv
023700         Write Output-Record from Msg03
023701         Move Spaces         to Out-Rec
023702         Move '* '           to Out-Rec (1:3)
023703         Move 'Cics:'        to Out-Rec (4:5)
023704         Move I-Target-Applid to Out-Rec (9:8)
023705         Move Commarea-Comm  to Out-Rec (18:61)
023710         Move '*'            to Out-Rec (80:1)
023720         Write Output-Record from Out-Rec
023800*        Perform the Link Request
024000         Exec Cics Link Program(Target-Program)
024100                        TransId(Target-TransId)
024200                        Applid(I-Target-Applid)
024300                        Commarea(Commarea)
024400                        Length(Length of Commarea)
024500                        DataLength(Length of Commarea-Data)
024600                        RetCode(Exci-Exec-Return-Code)
024700                        SyncOnReturn
024800         End-Exec
025000*        Check on how well the request has performed. We may have
025100*        to abort further processing if either the link, or the
025200*        server program failed in any way.
025400         If Exec-Resp = 0
025500           Perform Check-Server
025600         else
025700           Perform Link-not-ok
025710         end-If
025711       end-If
025712     end-Perform
025720     Write Output-record from Msg02
025730     Write Output-record from Msg31
025740     Close Printer Eingabe
025741     Move  Ceda-Sev-Highest to Return-Code
025742     If Abort
025743       Move 12  to Return-Code
025744     end-if
025760     stop run
026110     .
026200******************************************************************
026210*
026300*  The following code performs the required response checking
026400*  and will decide whether an abort is neccessary.
026500*
026510******************************************************************
026600 Check-Server Section.
026602     Move Spaces        to Out-Rec
026603     Move '*'           to Out-Rec (1:3)
026604     Move 'Returncode:' to Out-Rec (4:12)
026605     Move Ceda-Severity to Ceda-Sev-num
026607     Move Ceda-Sev-num  to Out-Rec (16:2)
026608     If Ceda-Sev-num > Ceda-Sev-Highest
026610       Move Ceda-Sev-num to Ceda-Sev-Highest
026611     end-If
026612     If Ceda-Severity > 0
026613        Move Ceda-Message  to Out-Rec (19:60)
026614     end-If
026615     Move '*'           to Out-Rec (80:1)
026616     Write Output-Record from Out-Rec
026617     Write Output-Record from Msg02
026620     .
029100 Check-Server-End.
029200     exit
029210     .
029220******************************************************************
029230*
029240*  The following code performs the required response checking
029250*  and will decide whether an abort is neccessary.
029260*
029270******************************************************************
029300 Link-not-ok Section.
029400     Write Output-Record from Msg11
029500     Move  Exec-Resp     to   O-Resp
029600     Move  Exec-Resp2    to   O-Resp2
029700     Move  Spaces        to   Opad-AbCode
029900     Move  Exec-Abcode   to   O-AbCode
030000     Write Output-Record from Output-Retarea
030100     If Exec-Msg-Ptr equal Nulls
030110       Continue
030120     else
030200       Write Output-Record from Msg13
030300       Write Output-Record from Msg02
030400       Set   Address of Exec-Level-Msg to Exec-Msg-ptr
030500       Move  Spaces to Out-Rec
030600       Perform test before varying Sub from 1 by 1
030800                           until Sub > Exec-Msg-Len
030900         Move Exec-Level-Msg-Text (Sub) to Out-Rec-Elem (Sub)
031000       end-Perform
031100       Write Output-Record from Out-Rec
031200       Write Output-Record from Msg02
031300     end-If
031400     Write Output-Record from Msg14
031500     Set Abort to true
031710     .
031720 Link-Not-ok-End.
031730     exit
031740     .
